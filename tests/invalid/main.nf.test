nextflow_pipeline {

    name "Test for invalid assembly sequence IDs"
    script "main.nf"

    test("protein fasta contains spaces - ignore gracefully - fail at invalid Fasta") {

        def assemblysheet_path = "$metaDir" + '/assemblysheet.csv'
        def assemblysheet_text = """\
            tag,fasta,is_masked
            invalid,$baseDir/tests/invalid/testdata/invalid.fasta.gz,no
            """.stripIndent()

        def proteins_path = "$metaDir" + '/proteins.txt'
        def proteins_text = """\

            https://raw.githubusercontent.com/nf-core/test-datasets/modules/data/genomics/sarscov2/genome/proteome.fasta.gz

            """.stripIndent()

        setup {
            (new File(assemblysheet_path)).text = assemblysheet_text
            (new File(proteins_path)).text = proteins_text
        }

        when {
            params {
                input               = assemblysheet_path
                protein_evidence    = proteins_path
                busco_skip          = true
                outdir              = "$outputDir"
            }
        }

        then {
            def stable_path = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore')

            assertAll(
                { assert workflow.success },
                { assert workflow.stdout.toString().contains("Invalid FASTA ID 'lcl|Lmh1Chr3' near line #6260: IDs must match [A-Za-z][A-Za-z0-9_]") },
                { assert snapshot(
                    [
                        'successful tasks': workflow.trace.succeeded().size(),
                        'versions': removeNextflowVersion("$outputDir/pipeline_info/genepal_software_mqc_versions.yml"),
                        'stable paths': stable_path
                    ]
                ).match() }
            )
        }

    }

    test("liftoff - dummy - same input output name - stub") {

        options '-stub'

        def assemblysheet_path = "$metaDir" + '/assemblysheet.csv'
        def assemblysheet_text = """\
            tag,fasta,is_masked
            dummy_genome,$baseDir/tests/invalid/testdata/dummy_genome.fasta,no
            """.stripIndent()

        def liftoff_path = "$metaDir" + '/liftoff.csv'
        def liftoff_text = """\
            fasta,gff3
            $baseDir/tests/invalid/testdata/dummy_ref.fasta,$baseDir/tests/invalid/testdata/dummy_ref.gff3
            """.stripIndent()

        setup {
            (new File(assemblysheet_path)).text = assemblysheet_text
            (new File(liftoff_path)).text = liftoff_text
        }

        when {
            params {
                input               = assemblysheet_path
                protein_evidence    = "$baseDir" + '/tests/invalid/testdata/dummy_proteins.faa'
                liftoff_annotations = liftoff_path
                busco_skip          = true
                outdir              = "$outputDir"
            }
        }

        then {
            def stable_path = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore')

            assertAll(
                { assert workflow.success },
                { assert snapshot(
                    [
                        'successful tasks': workflow.trace.succeeded().size(),
                        'versions': removeNextflowVersion("$outputDir/pipeline_info/genepal_software_mqc_versions.yml"),
                        'stable paths': stable_path
                    ]
                ).match() }
            )
        }
    }

}
